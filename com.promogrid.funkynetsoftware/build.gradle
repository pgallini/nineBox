
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven {
        url "https://maven.java.net/content/groups/public/"
    }
}

dependencies {
    compile 'com.sun.mail:android-mail:1.5.5'
    compile 'com.sun.mail:android-activation:1.5.5'
    compile 'com.android.support:support-v4:24.0.0'
    compile 'com.android.support:support-v13:24.0.0'
    compile 'com.android.support:cardview-v7:24.0.0'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:recyclerview-v7:24.0.0'
    compile 'com.github.amlcurran.showcaseview:library:5.4.3'
    compile 'com.google.android.gms:play-services-analytics:9.0.0'
}

// This build uses multiple directories to
// keep boilerplate and common code separate from
// the main sample code.
List<String> dirs = [
    'main',     // main sample code; look here for the interesting stuff.
    'common',   // components that are reused by multiple samples
    'template'] // boilerplate code that is generated by the sample template process


android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "com.promogrid.funkynetsoftware"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 10
        versionName "0.10"
    }

    signingConfigs {

        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    productFlavors {
        free {
            applicationId "com.promogrid.funkynetsoftware.free"
            versionName "promogrid-free 0.10"
            targetSdkVersion 23
            resValue "string", "appname", "Promo Grid"
            manifestPlaceholders = [
                    appIcon: "@drawable/ic_pg_icon"
                    ]
        }
        pro {
            applicationId "com.promogrid.funkynetsoftware.pro"
            versionName "promogrid-pro 0.10"
            targetSdkVersion 23
            resValue "string", "appname", "Promo Grid Pro"
            manifestPlaceholders = [
                    appIcon: "@drawable/ic_pg_pro_icon"
            ]
        }
    }
    productFlavors.all {
        flavor ->
            defaultConfig.buildConfigField 'String', 'var_'+flavor.name, '\"'+flavor.name+'\"'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main {
            dirs.each { dir ->
                java.srcDirs "src/${dir}/java"
                res.srcDirs "src/${dir}/res"
            }
        }
        androidTest.setRoot('tests')
        androidTest.java.srcDirs = ['tests/src']
        release {
            res.srcDir 'variants/release/res'
        }

        debug {
            res.srcDir 'variants/debug/res'
        }
    }
    packagingOptions {
        pickFirst 'META-INF/LICENSE.txt' // picks the JavaMail license file
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    buildTypes {
        debug {

        }
        release {
            signingConfig signingConfigs.release
        }

    }
}

apply plugin: 'com.google.gms.google-services'





